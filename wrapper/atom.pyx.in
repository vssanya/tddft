import numpy as np
cimport numpy as np

from types cimport cdouble
from wavefunc cimport ShWavefunc
from grid cimport ShGrid, ShNeGrid

import tdse.utils
if tdse.utils.is_jupyter_notebook():
    import matplotlib.pyplot as plt
    from IPython.core.pylabtools import print_figure

{{for Prefix in ["Sh", "ShNe"]}}
cdef class {{Prefix}}AtomCache:
    def __cinit__(self, Atom atom, {{Prefix}}Grid grid, double[::1] u = None):
        self.atom = atom
        self.grid = grid

        if u is None:
            self.cdata = new AtomCache[c{{Prefix}}Grid](atom.cdata[0], grid.data[0])
        else:
            self.cdata = new AtomCache[c{{Prefix}}Grid](atom.cdata[0], grid.data[0], &u[0], u.size)

    def __init__(self, Atom atom, {{Prefix}}Grid grid, double[::1] u = None):
        pass

    def __dealloc__(self):
        del self.cdata

    @property
    def u(self):
        return np.asarray(<double[:self.grid.Nr]>(self.cdata.data_u))

    @property
    def dudz(self):
        return np.asarray(<double[:self.grid.Nr]>(self.cdata.data_dudz))

    def _figure_data(self, format):
        fig, ax = plt.subplots()
        fig.set_size_inches((6,3))

        ax.plot(self.grid.r, self.u)

        ax.set_xlabel('r, (a.u.)')
        ax.set_ylabel('U, (a.u.)')

        ax.set_yscale('log')

        data = print_figure(fig, format)
        plt.close(fig)
        return data

    def _repr_png_(self):
        return self._figure_data('png')

    def write_params(self, params_grp):
        subgrp = params_grp.create_group('atom')

        self.atom.write_params(subgrp)

        subgrp.create_dataset('u'   , (self.grid.Nr,), dtype="d")[:] = self.u
        subgrp.create_dataset('dudz', (self.grid.Nr,), dtype="d")[:] = self.dudz
{{endfor}}

cdef class State:
    def __cinit__(self, const unsigned char[:] state=b"1s", int m=0, int countElectrons=0, int s=0):
        self.cdata = cAtom.cState(<char*>&state[0], m, countElectrons, s)
        pass

    def __init__(self, const unsigned char[:] state=b"1s", int m=0, int countElectrons=0, int s=0):
        pass

    @staticmethod
    cdef State from_c(cAtom.cState state):
        obj = <State>State.__new__(State)
        obj.cdata = state
        return obj

{{for field in ["l", "m", "n"]}}
    @property
    def {{field}}(self):
        return self.cdata.{{field}}

    @{{field}}.setter
    def {{field}}(self, value):
        self.cdata.{{field}} = value
{{endfor}}

    @property
    def countElectrons(self):
        return self.cdata.countElectrons


cdef class Atom:
    @staticmethod
    cdef Atom from_c(cAtom* atom, str name):
        obj = <Atom>Atom.__new__(Atom)
        obj.cdata = atom
        obj.name = name
        obj.ground_state = State.from_c(atom.groundState)
        return obj

    def __dealloc__(self):
        del self.cdata

    @property
    def countOrbs(self):
        return self.cdata.countOrbs

    @property
    def countElectrons(self):
        return self.cdata.countElectrons

    @property
    def l_max(self):
        cdef int lmax = 0
        cdef int i = 0

        for i in range(self.cdata.countOrbs):
            if lmax < self.cdata.orbs[i].l:
                lmax = self.cdata.orbs[i].l

        return lmax

    @property
    def orbCountElectrons(self):
        res = np.zeros(self.countOrbs)
        for i in range(res.size):
            res[i] = self.getCountElectrons(i)

        return res

    def get_l(self, int i):
        assert(i<self.cdata.countOrbs)
        return self.cdata.orbs[i].l

    def get_m(self, int i):
        assert(i<self.cdata.countOrbs)
        return self.cdata.orbs[i].m

    def getCountElectrons(self, int i):
        return self.cdata.orbs[i].countElectrons

    def getNumberOrt(self, int i):
        return self.cdata.getNumberOrt(i)

    def getActiveOrbs(self, int shell, np.ndarray[np.uint8_t, ndim=1] active_orbs = None):
        cdef bool_t* active_orbs_ptr = NULL
        if active_orbs is None:
            active_orbs = np.ndarray(self.cdata.countOrbs, np.uint8)

        active_orbs_ptr = <bool_t*>active_orbs.data
        self.cdata.getActiveOrbs(shell, active_orbs_ptr)

        return active_orbs

    @property
    def countShells(self):
        return self.cdata.getNumberShell()

    @property
    def Z(self):
        return self.cdata.Z

    def u(self, double r):
        return self.cdata.u(r)

    def dudz(self, double r):
        return self.cdata.dudz(r)

    def write_params(self, params_grp):
        params_grp.attrs['type'] = self.name


{{for Prefix in ['H', 'He', 'HSmoth', 'Ne', 'Ar', 'Kr', 'Xe', 'Rb', 'Cs', 'Ba', 'BaP', 'CsP', 'Ba2P', 'ArSae', 'ArSaeSmooth', 'Na', 'Mg']}}
{{Prefix}} = Atom.from_c(<cAtom*> new {{Prefix}}Atom(), "{{Prefix}}")
{{endfor}}

Fm = Atom.from_c(<cAtom*> new FNegativeIon(), "F-")
FmSae = Atom.from_c(<cAtom*> new FNegativeSaeIon(), "F-_sae")

{{for atom in ['Na', 'He', 'Ne', 'Li', 'Rb']}}
{{atom}}Sae   = Atom.from_c(<cAtom*> new {{atom}}AtomSGB(), "{{atom}}_sae")
{{endfor}}

ArSaeSGB = Atom.from_c(<cAtom*> new ArAtomSGB(), "ArSaeSGB")

C60 = Atom.from_c(<cAtom*> new Fulleren(), "Fulleren")

NONE = Atom.from_c(<cAtom*> new NoneAtom(), "None")

def getShortAtom(double c, double n):
    return Atom.from_c(<cAtom*> new ShortAtom(c, n), "ShortAtom_c{}_n{}".format(c, n))
